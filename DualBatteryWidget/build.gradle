apply plugin: 'android'

configurations {
    compile
    androidannotations.extendsFrom(compile)
}

dependencies {
    compile project(':BatteryEngine')
    compile project(':AChartEngine')
    compile project(':ViewPagerIndicator')
    androidannotations('org.androidannotations:androidannotations:3.0.1')
    compile('org.androidannotations:androidannotations-api:3.0.1')
    compile('com.android.support:support-v4:13.0.0')
    compile('com.actionbarsherlock:actionbarsherlock:4.4.0@aar')
    compile('de.greenrobot:greendao:1.3.1')
    compile('net.robotmedia:billing-local:1.0')
    compile('org.flexlabs:androidextensions:0.3.2')
    compile('org.flexlabs:androidextensions-sherlock:0.3.2')
}

def annotationDirs = file('src/main/java-agen')

android {
    compileSdkVersion 20
    buildToolsVersion '20'

    defaultConfig {
        versionCode 34
        versionName '2.0.4'
    }
    signingConfigs {
        release
    }
    buildTypes {
        debug {
            applicationIdSuffix = '.debug'
            versionNameSuffix = '-PRE-RELEASE'
        }
    }
    dexOptions {
        preDexLibraries = false
    }
    lintOptions {
        abortOnError false
    }
}
if (project.hasProperty('signingStoreLocation')) {
    android.signingConfigs.release {
        storeFile file(signingStoreLocation)
        storePassword signingStorePassword
        keyAlias signingKeyAlias
        keyPassword signingKeyPassword
    }
    android.buildTypes.release.signingConfig = android.signingConfigs.release
}

task annotationsDir {
    outputs.dir annotationDirs

    doFirst {
        if (!annotationDirs.isDirectory()) {
            println 'Creating: ' + annotationDirs
            annotationDirs.mkdirs()
        }
    }
}

tasks.clean.dependsOn tasks.cleanAnnotationsDir

android.applicationVariants.all { variant ->
    variant.javaCompile.dependsOn project.project(':DaoGenerator').tasks['daoGen']
    variant.javaCompile.dependsOn annotationsDir
    variant.javaCompile.options.compilerArgs += [
            '-processorpath', configurations.androidannotations.asPath,
            '-processor', 'org.androidannotations.AndroidAnnotationProcessor',
            '-s',annotationDirs
    ]
}

