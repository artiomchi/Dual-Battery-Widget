apply plugin: 'android'

repositories {
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

configurations {
    compile
    androidannotations.extendsFrom(compile)
    all*.exclude group: 'com.google.android', module: 'support-v4'
}

dependencies {
    compile project(':AChartEngine')
    compile project(':ViewPagerIndicator')
    androidannotations('org.androidannotations:androidannotations:3.0-SNAPSHOT')
    compile('org.androidannotations:androidannotations-api:3.0-SNAPSHOT')
    compile('com.android.support:support-v4:13.0.0')
    compile('com.actionbarsherlock:actionbarsherlock:4.4.0@aar')
    compile('de.greenrobot:greendao:1.3.1')
    compile('net.robotmedia:billing-local:1.0')
    compile('org.flexlabs:androidextensions:0.3.2')
    compile('org.flexlabs:androidextensions-sherlock:0.3.2')
}

def annotationDirs = file('src/main/java-agen')

android {
    compileSdkVersion 18
    buildToolsVersion '18.1'

    defaultConfig {
        versionCode 30
        versionName '1.1.8'
    }
    signingConfigs {
        release
    }
    buildTypes {
        debug {
            packageNameSuffix = '.debug'
        }
    }
}
if (project.hasProperty('signingStoreLocation')) {
    android.signingConfigs.release {
        storeFile file(signingStoreLocation)
        storePassword signingStorePassword
        keyAlias signingKeyAlias
        keyPassword signingKeyPassword
    }
    android.buildTypes.release.signingConfig = android.signingConfigs.release
}

task annotationsDir {
    outputs.dir annotationDirs

    doFirst {
        if (!annotationDirs.isDirectory()) {
            println 'Creating: ' + annotationDirs
            annotationDirs.mkdirs()
        }
    }
}

tasks.clean.dependsOn tasks.cleanAnnotationsDir

android.applicationVariants.all { variant ->
    variant.javaCompile.dependsOn project.project(':DaoGenerator').tasks.daoGen
    variant.javaCompile.dependsOn annotationsDir
    variant.javaCompile.options.compilerArgs += [
            '-processorpath', configurations.androidannotations.asPath,
            '-processor', 'org.androidannotations.AndroidAnnotationProcessor',
            '-s',annotationDirs
    ]
}

